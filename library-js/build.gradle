apply plugin: "com.moowork.node"
apply plugin: 'kotlin2js'

compileKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.sourceMap = true
    kotlinOptions.suppressWarnings = true
    kotlinOptions.verbose = true
    kotlinOptions.main = "call"
    kotlinOptions.moduleKind = "umd"
}

def outputDir = "${projectDir}/node_modules"

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js"
    compile project(':library')
}

task assembleJs(type: Sync) {
    println(">>> assembleJs")
    configurations.compile.each { File file ->
        println(">>> each file: ${file}")
        println(">>> each from: ${zipTree(file.absolutePath)}")
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") ||
                        !path.startsWith("META-INF/"))
            }
        })
    }
    println(">>> from: ${compileKotlin2Js.destinationDir}")
    from compileKotlin2Js.destinationDir
    println(">>> into: ${outputDir}")
    into "${outputDir}"
    dependsOn classes
}

// wont be needed once https://github.com/ktorio/ktor/issues/961 is fixed
task installTextEncode(type: NpmTask, dependsOn: [npmInstall]) {
    // install the text-encoding package only
    args = ['install', 'text-encoding']
}

task webpack(type: NodeTask, dependsOn: [npmInstall, installTextEncode]) {
    script = file("$projectDir/node_modules/webpack/bin/webpack")
}

assemble.dependsOn assembleJs
webpack.dependsOn assemble

node {
    version = "$node_version"
    download = false
}

clean.doFirst() {
    delete "$outputDir"
}